@echo off

:global-settings
chcp 65001
cls
cd /D "%~dp0"
setlocal enabledelayedexpansion
set "VENV_HOME=.venv"
set "PYTHON_HOME=%VENV_HOME%/Scripts/"
set "PYTHON_RECOMMENDED_VERSION=3.10.6"
set "MODELS_HOME=./models/"
set "LOGGING_PREFIX=[90m[[33mCoolCLK[90m/[36mturn-live-photos[90m] [0m"

:check-python
cmd /c "exit /b 0"
where python>nul
if not %errorlevel% equ 0 (
    echo %LOGGING_PREFIX%æœªæ£€æµ‹åˆ° Python ç¯å¢ƒ
    goto end
) else (
    for /F "delims=" %%i in ('python --version') do ( set "PYTHON_VERSION=%%i" )
    if not "!PYTHON_VERSION!"=="Python %PYTHON_RECOMMENDED_VERSION%" (
        echo %LOGGING_PREFIX%è­¦å‘Šï¼šä½ æ­£åœ¨ä½¿ç”¨ä¸€ä¸ªä¸è¢«æ”¯æŒçš„ Python ç‰ˆæœ¬ !PYTHON_VERSION!
        echo %LOGGING_PREFIX%æˆ‘ä»¬æ¨èä½¿ç”¨ Python %PYTHON_RECOMMENDED_VERSION%
        echo %LOGGING_PREFIX%è¯¦è§ https://www.python.org/downloads/release/python-3106/ 
    )
)

:activate-venv
if not exist %VENV_HOME% (
    echo %LOGGING_PREFIX%æ­£åœ¨åˆ›å»ºè™šæ‹Ÿç¯å¢ƒä¸­
    python -m venv %VENV_HOME%>nul 1>nul
    echo %LOGGING_PREFIX%åœ¨ %VENV_HOME% ç›®å½•åˆ›å»ºäº†è™šæ‹Ÿç¯å¢ƒ
)
echo %LOGGING_PREFIX%æ¿€æ´»è™šæ‹Ÿç¯å¢ƒä¸­...
call %VENV_HOME%\Scripts\activate.bat

:ask-requirements
set /p "DO_CHECK_REQUIRMENTS=%LOGGING_PREFIX%æ˜¯å¦æ£€æŸ¥ä¾èµ– (Y/n) "
if /i "%DO_CHECK_REQUIRMENTS%"=="Y" (
    echo %LOGGING_PREFIX%ä» requirements.txt å¤„ç†ä¾èµ–ä¸­
    set /p=[31m<nul
    python -m pip install --upgrade pip -i https://pypi.tuna.tsinghua.edu.cn/simple>nul 1>nul
    "%PYTHON_HOME%/pip3.exe" install torch==2.7.0+cu128 torchvision torchaudio --index-url https://download.pytorch.org/whl/cu128>nul 1>nul
    "%PYTHON_HOME%/pip.exe" install -r requirements.txt -i https://pypi.tuna.tsinghua.edu.cn/simple>nul 1>nul
    set /p=[0m<nul
)

:pretrained-model-downloading
set /p "DO_DOWNLOAD_MODEL=%LOGGING_PREFIX%æ˜¯å¦æå‰ä¸‹è½½/æ›´æ–°æ¨¡å‹ (Y/n) "
if /i "%DO_DOWNLOAD_MODEL%"=="Y" (
    git --version>nul
    set "MODEL_URL=https://huggingface.co/stabilityai/stable-video-diffusion-img2vid-xt"
    if not %errorlevel% equ 9009 (
        echo %LOGGING_PREFIX%æˆ‘ä»¬é»˜è®¤ä½ å·²å®‰è£… Git LFSï¼ˆhttps://git-lfs.com/ï¼‰

        set /p "DO_USE_MODEL_MIRROR_URL=%LOGGING_PREFIX%æ˜¯å¦ä½¿ç”¨ hf-mirror.com é•œåƒ (Y/n) "
        if /i "!DO_USE_MODEL_MIRROR_URL!"=="Y" (set "MODEL_URL=https://hf-mirror.com/stabilityai/stable-video-diffusion-img2vid-xt")
        set "GIT_ACTION=clone"
        if exist "%MODELS_HOME%stabilityai/stable-video-diffusion-img2vid-xt" (set "GIT_ACTION=pull") else (mkdir "%MODELS_HOME%stabilityai">nul)
        cd /D "%MODELS_HOME%stabilityai"
        echo %LOGGING_PREFIX%å¼€å§‹æ‹‰å– stabilityai/stable-video-diffusion-img2vid-xt ...
        set /p "=[31m"<nul
        git lfs install>nul 1>nul
        git !GIT_ACTION! !MODEL_URL!>nul 1>nul
        set /p "=[0m"<nul
        cd /D "%~dp0"
    ) else (
        echo %LOGGING_PREFIX%æ£€æµ‹åˆ°æœªå®‰è£… Gitï¼ˆhttps://git-scm.com/ï¼‰ï¼Œæ­£åœ¨è·³è¿‡...
    )
)

:run-script
set /p "ARGUMENTS="<run_args.txt
if "%ARGUMENTS%"=="" (echo %LOGGING_PREFIX%å¯åŠ¨ Python è„šæœ¬) else (echo %LOGGING_PREFIX%ä»¥ %ARGUMENTS% çš„å‚æ•°å¯åŠ¨ Python è„šæœ¬)
"%PYTHON_HOME%/python.exe" app.py %ARGUMENTS%
echo %LOGGING_PREFIX%Python è„šæœ¬å·²åœæ­¢

:deactivate-venv
echo %LOGGING_PREFIX%åœç”¨è™šæ‹Ÿç¯å¢ƒä¸­
call %VENV_HOME%\Scripts\deactivate.bat

:end
echo %LOGGING_PREFIX%å…³é—­è„šæœ¬ä¸­