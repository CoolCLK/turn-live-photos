@echo off

:global-settings
chcp 65001>nul
cd /D "%~dp0"
setlocal enabledelayedexpansion

set "PYTHON_HOME="
set "PYTHON_RECOMMENDED_VERSION=3.10.6
set "MODELS_HOME=./models/"
set "LOGGING_PREFIX=[90m[[33mCoolCLK[90m/[36mturn-live-photos[90m] [0m"

:read-configuration
for /f "delims=" %%l in (configuration.ini) do (
    set "_=%%l"
    if "!_:~0,1!"=="[" (
        set "CATEGORY=!_:~1,-1!"
    ) else if not "!_:~0,1!"=="#" (
        for /f "tokens=1,2 delims==" %%i in ("%%l") do (
            set "!CATEGORY!.%%i=%%j"
        )
    )
    set "_="
)

:check-python
if /i not "!LaunchOptions.SkipPythonChecking!"=="True" (
    cmd /c "exit /b 0"
    where python>nul
    if not %errorlevel% equ 0 (
        echo %LOGGING_PREFIX%æœªæ£€æµ‹åˆ° Python ç¯å¢ƒ
        goto end
    ) else (
        for /f "delims=" %%i in ('python --version') do ( set "PYTHON_VERSION=%%i" )
        if not "!PYTHON_VERSION!"=="Python %PYTHON_RECOMMENDED_VERSION%" (
            echo %LOGGING_PREFIX%è­¦å‘Šï¼šä½ æ­£åœ¨ä½¿ç”¨ä¸€ä¸ªä¸è¢«æ”¯æŒçš„ Python ç‰ˆæœ¬ !PYTHON_VERSION!
            echo %LOGGING_PREFIX%æˆ‘ä»¬æ¨èä½¿ç”¨ Python %PYTHON_RECOMMENDED_VERSION%
            echo %LOGGING_PREFIX%è¯¦è§ https://www.python.org/downloads/release/python-3106/ 
        )
    )
)

:activate-venv
if /i "!LaunchOptions.UseVenv!"=="True" (
    if not exist !StoreOptions.VenvHome! (
        echo %LOGGING_PREFIX%æ­£åœ¨åˆ›å»ºè™šæ‹Ÿç¯å¢ƒä¸­
        "%PYTHON_HOME%python.exe" -m venv !StoreOptions.VenvHome!>nul 1>nul
        echo %LOGGING_PREFIX%åœ¨ !StoreOptions.VenvHome! ç›®å½•åˆ›å»ºäº†è™šæ‹Ÿç¯å¢ƒ
        set "PYTHON_HOME=!StoreOptions.VenvHome!/Scripts/"
    )
    echo %LOGGING_PREFIX%æ¿€æ´»è™šæ‹Ÿç¯å¢ƒä¸­...
    call !StoreOptions.VenvHome!\Scripts\activate.bat
)

:ask-requirements
set /p "DO_CHECK_REQUIRMENTS=%LOGGING_PREFIX%æ˜¯å¦æ£€æŸ¥ä¾èµ– (Y/n) "
if /i "%DO_CHECK_REQUIRMENTS%"=="Y" (
    set "InstalltionOptions.PipMirrorArguments="
    if not "!InstalltionOptions.PyTorchIndexUrl!"=="" (
        set "InstalltionOptions.PipMirrorArguments=!InstalltionOptions.PipMirrorArguments! --extra-index-url !InstalltionOptions.PyTorchIndexUrl!"
    )
    if not "!InstalltionOptions.PipMirrorUrl!"=="" (
        set "InstalltionOptions.PipMirrorArguments=!InstalltionOptions.PipMirrorArguments! -i !InstalltionOptions.PipMirrorUrl!"
    )
    echo %LOGGING_PREFIX%ä» requirements.txt å¤„ç†ä¾èµ–ä¸­
    set /p=[31m<nul
    python -m pip install --upgrade pip!InstalltionOptions.PipMirrorArguments!>nul 1>nul
    "!PYTHON_HOME!pip.exe" install -r requirements.txt !InstalltionOptions.PipMirrorArguments!>nul 1>nul
    set /p=[0m<nul
)

:pretrained-model-downloading
set /p "DO_DOWNLOAD_MODEL=%LOGGING_PREFIX%æ˜¯å¦æå‰ä¸‹è½½/æ›´æ–°æ¨¡å‹ (Y/n) "
if /i "%DO_DOWNLOAD_MODEL%"=="Y" (
    git --version>nul
    if not %errorlevel% equ 9009 (
        echo %LOGGING_PREFIX%æˆ‘ä»¬é»˜è®¤ä½ å·²å®‰è£… Git LFSï¼ˆhttps://git-lfs.com/ï¼‰

        set "GIT_ACTION=clone"
        if exist "!StoreOptions.ModelsHome!/!ModelOptions.ModelName!" (set "GIT_ACTION=pull") else (mkdir "!StoreOptions.ModelsHome!/!ModelOptions.ModelName!">nul)
        cd /D "!StoreOptions.ModelsHome!/!ModelOptions.ModelName!/../"
        echo %LOGGING_PREFIX%å¼€å§‹æ‹‰å– !ModelOptions.ModelName! ...
        set /p "=[31m"<nul
        git lfs install>nul 1>nul
        git !GIT_ACTION! !InstalltionOptions.ModelRepositoryUrl!>nul 1>nul
        set /p "=[0m"<nul
        cd /D "%~dp0"
    ) else (
        echo %LOGGING_PREFIX%æ£€æµ‹åˆ°æœªå®‰è£… Gitï¼ˆhttps://git-scm.com/ï¼‰ï¼Œæ­£åœ¨è·³è¿‡...
    )
)

:run-script
if "!LaunchOptions.RunArguments!"=="" (echo %LOGGING_PREFIX%å¯åŠ¨ Python è„šæœ¬) else (echo %LOGGING_PREFIX%ä»¥ !LaunchOptions.RunArguments! çš„å‚æ•°å¯åŠ¨ Python è„šæœ¬)
"!PYTHON_HOME!python.exe" app.py !LaunchOptions.RunArguments!
echo %LOGGING_PREFIX%Python è„šæœ¬å·²åœæ­¢

:deactivate-venv
if /i "!LaunchOptions.UseVenv!"=="True" (
    echo %LOGGING_PREFIX%åœç”¨è™šæ‹Ÿç¯å¢ƒä¸­
    call !StoreOptions.VenvHome!\Scripts\deactivate.bat
)

:end
echo %LOGGING_PREFIX%å…³é—­è„šæœ¬ä¸­